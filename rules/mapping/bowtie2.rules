rule bowtie2_index:
    input:
        "references/{reference}.fna"
    output:
        expand("mapping/bowtie2/{{reference}}.{index}.bt2", index=range(1,5)),
        expand("mapping/bowtie2/{{reference}}.rev.{index}.bt2", index=range(1,3))
    params:
        prefix="mapping/bowtie2/{reference}",
        load_env = config["bowtie2_rules"]["load_env"],
        out_dir = "mapping/bowtie2",
    shell:
        """{params.load_env}
            bowtie2-build {input} {params.prefix}
            mkdir -p {params.out_dir}
        """

rule bowtie2_map:
    input:
        fastq1 = "samples/after_qc/{sample}_R1.fastq",
        fastq2 = "samples/after_qc/{sample}_R2.fastq",
        bt2_index1 = expand("mapping/bowtie2/{{reference}}.{index}.bt2", index=range(1,5)),
        bt2_index2 = expand("mapping/bowtie2/{{reference}}.rev.{index}.bt2", index=range(1,3))
    output:
        temp("mapping/bowtie2/{reference}/{sample}/{sample}.bam")
    params:
        ref_idx_base="mapping/bowtie2/{reference}",
        load_env = config["bowtie2_rules"]["load_env"],
        out_dir = "mapping/bowtie2/{reference}/{sample}"
    log:
        "mapping/bowtie2/{reference}/{sample}/{sample}.bam.log"
    shell:
        """{params.load_env}
        mkdir -p {params.out_dir}
        bowtie2 \
        -x {params.ref_idx_base} \
        -p {threads} \
        -1 {input.fastq1} \
        -2 {input.fastq2} \
        2> {log} | \
        samtools view -Sbh - > {output}
        """
